<!DOCTYPE html>

<html>
<head>
	<title>Processee</title>

	<link rel="stylesheet" type="text/css" href="./style/style.css">
	<link rel="stylesheet" type="text/css" href="./style/codemirror.css">
	<link rel="stylesheet" type="text/css" href="./style/processee-light.css">

	<script type="text/javascript" src="./lib/jquery.min.js"></script>
	<script type="text/javascript" src="./lib/processing.min.js"></script>
	<script type="text/javascript" src="./lib/coffee-script.min.js"></script>
	<script type="text/javascript" src="./lib/codemirror.js"></script>
	<script type="text/javascript" src="./lib/mode-processee.js"></script>
	<script type="text/javascript" src="./lib/FileSaver.min.js"></script>
	<script type="text/javascript" src="./lib/BlobBuilder.min.js"></script>
	<script type="text/javascript" src="./lib/canvas-toBlob.min.js"></script>

	<script type="text/javascript" src="./js/main.js"></script>
	<script type="text/javascript" src="./js/processee.js"></script>
</head>

<body>
	<div class="editor">
		<textarea id="codemirror"></textarea>
	</div>

	<div class="output" id="output">
		<div class="webcam">
			<video id="webcam"></video>
		</div>
		<canvas class="output" id="processing"></canvas>
		<div class="output-buttons">
			<a class="button small save-image" onclick="saveOutput();"></a>
		</div>
	</div>

	<div class="buttons">
		<a class="button run-image" onclick="processee.run();"></a>
		<a class="button save-image" onclick="saveSketch();"></a>
		<a class="button info-image" onclick="toggleAbout();"></a>
	</div>

	<div class="internals">
		<canvas id="processee-internal-canvas"></canvas>
		<img id="processee-image-loader"></img>
		<div class="examples">

<div class="example" id="ex-flower">
# Flower
# Draw a pretty flower.

processee.setup ->
  @canvasSize = dim 300, 300
  @canvasBackground = gray 255

processee.once ->
  @at {x: 150, y: 250}, stalk
  @at {x: 150, y: 100}, petals
  @at {x: 175, y: 210}, leaf
  @at {x: 125, y: 230}, leaf

stalk = ->
  @fillColor = rgb 0, 255, 0
  @strokeColor = rgb 0, 255, 0
  @drawRect {width: 10, height: 200}

leaf = ->
  @fillColor = rgb 0, 255, 0
  @strokeColor = rgb 0, 255, 0
  @drawEllipse {width: 40, height: 20}

petals = ->
  @transparency = 0.2
  for angle in [0, Math.PI/3, Math.PI/6]
    @rotatedBy angle, petal

petal = ->
  @fillColor = rgb 255, 0, 0
  @strokeColor = rgb 255, 0, 0
  @drawSquare {size: 100}
</div>

<div class="example" id="ex-image">
# Image
# Load and display an image.

image = "monalisa.jpg"

processee.setup ->
  @loadImage image

processee.once ->
  @canvasSize = @sizeOf image
  @drawImage image
</div>

<div class="example" id="ex-overlay">
# Overlay
# Draw transparent images.

image = "monalisa.jpg"
logo = "ui/save.png"

processee.setup ->
  @loadImage image
  @loadImage logo

processee.once ->
  @canvasSize = @sizeOf image
  @drawImage image
  @at {x:113, y:80}, -> @drawImage logo
</div>

<div class="example" id="ex-animate">
# Animate
# Demonstrates frame-by-frame animation.

processee.setup ->
  @canvasSize = dim 300, 300
  @canvasBackground = gray 255

x = 0
y = 0
dx = 1.3
dy = 0.9

processee.everyFrame ->
  # Uncomment this to disable the trail:
  #@canvasBackground = gray 255
  @drawSquare {
    x: x
    y: y
    size: 20
  }
  x = x + dx
  y = y + dy
  dx = if x < 0 or x > 300 then -dx else dx
  dy = if y < 0 or y > 300 then -dy else dy
</div>

<div class="example" id="ex-pixelate">
# Pixelate
# Simple pixelation effect.

image = "monalisa.jpg"
pixelated = "pixelated"

pixelSize = 5

processee.setup ->
  @loadImage image

processee.once ->
  @canvasSize = @sizeOf image
  @makeNewImage pixelated, image
  @forEachPixelOf pixelated, (pixel) ->
    pixel.x = pixel.x - (pixel.x % pixelSize)
    pixel.y = pixel.y - (pixel.y % pixelSize)
    return @getImagePixel pixelated, pixel
  @drawImage pixelated
</div>

<div id="ex-pixelatemask">
# See example focalpixelate instead.
</div>

<div id="ex-focalpixelate">
# Focal pixelate
# Assignment code for COMP3419.
# This effect pixelates an image a variable amount. At the 'focal point'
# there is no pixelation. Regions further from the focal point are more
# and more pixelated. The pixels get bigger the further you go.
# Inspired by PicMonkey's Focal Pixelate effect.
# Daniel Buckmaster, 2013

# Define options for the landscape image.
landscape =
  # mage file loaded from server.
  image: "landscape.jpg"
  # Maximum pixelation (i.e. 10 pixels/pixel).
  maxPixelSize: 10
  # Falloff radius of the pixelation effect. Smaller values mean a smaller
  # unblurred area.
  radius: 70
  # Location of the unblurred point (fraction of each dimension).
  focalPoint:
    x: 1
    y: 0.5

# Define options for lenna.
lenna =
  image: "lenna.jpg"
  maxPixelSize: 10
  radius: 30
  focalPoint: {x: 0.55, y: 0.6}

# Change this to try different images.
settings = landscape

# Asynchronously load the source image.
processee.setup ->
  @loadImage settings.image

# Main function.
processee.once ->
  @do pixelate

# The pixelate routine.
pixelate = ->
  image = settings.image
  # Scale the canvas to the size of the image.
  @canvasSize = @sizeOf image
  # Get the pixel coordinates of the focal point.
  focusX = (@sizeOf image).width  * settings.focalPoint.x
  focusY = (@sizeOf image).height * settings.focalPoint.y
  # This helper calls a function on every pixel in the image, and puts the
  # return values into a new image map of the same dimensions. After all
  # pixels are processed, the old image data is replaced with the new.
  @forEachPixelOf image, (pixel) ->
    # First, calculate the distance (in pixels) to the focal point.
    dist = Math.sqrt(Math.pow(focusX - pixel.x, 2) +
           Math.pow(focusY - pixel.y, 2))
    # Scale it a bit and round down.
    dist = Math.floor(dist / settings.radius)
    # In CoffeeScript, the result of the last statement in a function is
    # Returned. In this case we return the result of an if statement:
    if dist is 0
      # For pixels very close to the focus, return their unmodified value.
      pixel
    else
      # Calculate the pixel size we want at this radius.
      size = Math.min(settings.maxPixelSize, dist)
      # Calculate the coordinates to read from by modding the coordinates
      # of the current pixel by the pixel size. This creates the pixelation
      # effect (see the '#pixelate' example).
      dx = pixel.x - (pixel.x % size)
      dy = pixel.y - (pixel.y % size)
      # Return the value of the pixel at the new coordinates.
      @getImagePixel image, dx, dy
  # Finally, draw the transformed image.
  @drawImage image
</div>

<div id="ex-webcam">
# Webcam
# Stare at your own face in your browser!
processee.setup ->
  @webcam = on

processee.once ->
  @canvasSize = @sizeOf "webcam"

processee.everyFrame ->
  @drawImage "webcam"
</div>

<div class="example" id="ex-photobooth">
# Photobooth
# Capturing still images from the webcam.

processee.setup ->
  @webcam = on
  @webcamImageName = "webcam"

# Press Ctrl-Enter or the run button to take a snapshot!
processee.once ->
  @canvasSize = @sizeOf "webcam"
  @drawImage "webcam"
</div>

<div class="example" id="ex-threshold">
# Threshold
# A threshold filter.

image = "lenna.jpg"

processee.setup ->
  @loadImage image

processee.once ->
  @canvasSize = @sizeOf image
  @forEachPixelOf image, threshold
  @drawImage image

threshold = (pixel) ->
  if pixel.red + pixel.green + pixel.blue > 384
    gray 255
  else
    gray 0
</div>

<div class="example" id="ex-pixels">
# Pixels
# Create your own image with pixel data.

processee.once ->
  @makeNewImage "img", { width: 255, height: 255 }
  @canvasSize = @sizeOf "img"
  @forEachPixelOf "img", chooseColor
  @drawImage "img"

chooseColor = (pixel) ->
  gray ((pixel.x + pixel.y) % 255)
</div>

<div class="example" id="ex-blur">
# Blur
# A simple image blur algorithm.

image = "lenna.jpg"

processee.setup ->
  @loadImage image

processee.once ->
  @canvasSize = @sizeOf image
  @forEachPixelOf image, @convolveWith {
    matrix: [
      1, 1, 1,
      1, 1, 1,
      1, 1, 1,
    ]
    scale: 1/9
  }
  @drawImage image
</div>

<div class="example" id="ex-sharpen">
# Sharpen
# Uses the unsharp mask algorithm.

image = "lenna.jpg"

processee.setup ->
  @loadImage image

processee.once ->
  @canvasSize = @sizeOf image
  @forEachPixelOf image, @convolveWith [
     0, -1,  0,
    -1,  5, -1,
     0, -1,  0,
  ]
  @drawImage image
</div>

<div class="example" id="ex-median">
# Median
# An example of a median blur filter.

image = "monalisa.jpg"

processee.setup ->
  @loadImage image

processee.once ->
  @canvasSize = @sizeOf image
  @forEachPixelOf image, median
  @drawImage image

median = (pixel) ->
  reds = []
  greens = []
  blues = []
  @forEachNeighborOf pixel, (n) -> reds.push n.red
  @forEachNeighborOf pixel, (n) -> greens.push n.green
  @forEachNeighborOf pixel, (n) -> blues.push n.blue
  reds = sort reds
  greens = sort greens
  blues = sort blues
  rgb reds[4], greens[4], blues[4]
</div>

<div class="example" id="ex-objects">
# Objects
# Add objects to your scene.

class Square
  constructor: (@pos, @vel) ->
  draw: (self) ->
    self.pos.x += self.vel.x
    self.pos.y += self.vel.y
    @at self.pos, ->
      @strokeColor = rgb 0
      @fillColor = rgb 255
      @drawSquare {size: 20}

s = new Square {x:0,y:0}, {x:1,y:1}
p = new Square {x:0,y:300}, {x:1,y:-1}

processee.objects [s, p]

processee.setup ->
  @canvasSize = dim 300, 300

processee.everyFrame {layer:0}, ->
  @canvasBackground = gray 255
</div>

<div class="example" id="ex-mouse">
# Mouse
# Demonstrates mouse movement and click capturing.

processee.setup ->
  @canvasSize = dim 300, 300
  @canvasBackground = gray 255

processee.onMouseMove (event) ->
  @drawLine
    to:
      x: event.x
      y: event.y
    # The @mouse variable is only changed after this event, so the old mouse
    # position is still in it.
    from:
      x: @mouse.x
      y: @mouse.y

processee.onClick (event) ->
  @drawSquare
    x: event.x
    y: event.y
    size: 20
</div>
		</div>
	</div>
	<div class="about">
		<div class="dialog">
			<h2>Demos</h2>
			<ul>
			</ul>
			<p>
				Code available on
				<a href="http://github.com/eightyeight/processee">GitHub</a>
			</p>
			<p>
				Interface buttons from
				<a href="http://www.picol.org/index.php">PICOL</a>
			</p>
		</div>
	</div>
</body>
</html>
